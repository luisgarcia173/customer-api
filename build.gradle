plugins {
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
}

group = 'com.builders.customer'
version = '1.0.0'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	// SpringBoot Starter
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Lombok Annotations
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Swagger
	implementation "io.springfox:springfox-boot-starter:3.0.0"
	compile "io.springfox:springfox-swagger-ui:3.0.0"

	// DTO Mapper
	compile 'org.modelmapper:modelmapper:2.4.0'

	// H2 Driver
	compile 'com.h2database:h2:1.4.200'
}

jacoco {
	toolVersion = "0.8.6"
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			excludes = [
				'com.builders.customer.resources.handler.*',
				'com.builders.customer.resources.dtos.*',
				'com.builders.customer.repositories.entities.*'
			]
			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.9
			}
		}
	}
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacocoHtml")
	}
	dependsOn test
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'com/builders/customer/resources/handler/**',
				'com/builders/customer/resources/dtos/**',
				'com/builders/customer/repositories/entities/**'
			])
		}))
	}
	finalizedBy jacocoTestCoverageVerification
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
